#=======================================================================================================================================
# This is the code for paper title "Modulation of signaling cross-talk between pJNK and pAKT generates optimal apoptotic response".
#=======================================================================================================================================
# Authors: Sharmila Biswas(1,¶), Baishakhi Tikader(2,¶), Sandip Kar(2,*), Ganesh Viswanathan(1,*)
# ¶ These authors contributed equally to this work# 1 Department of Chemical Engineering, Indian Institute of Technology Bombay, Mumbai, India.# 2 Department of Chemistry, Indian Institute of Technology Bombay, Mumbai, India.# This code has been generated by Baishakhi Tikader#=======================================================================================================================================
#=======================================================================================================================================
# Ordinary differential equations for the model proposed in Fig 2B.
# This code can be run in XPPAUT to generate the Fig 3 and Fig 4 of the main text. 
# This code can also be used in XPPAUT to reproduce the S10 Fig, S11 Fig, Fig II in S3 Text.
#=======================================================================================================================================
#=======================================================================================================================================
#Initial conditions
init pJNK=1
init pAKT=1
init pERK=1
init Csps3a=1
#========================================================================================================================================
# Differential equations of the model
#========================================================================================================================================
# JNK module
#----------------------------------------------------------------------------------------------------------------------------------------
dTNFR1a/dt= Kbase + (Ktnf1*TNFR1*TNF)-(Ktnf2*TNFR1a)
dC1Pa/dt=(Kbc1*C1P)+(Kcac1*C1P*CERa)-(Kcac2*C1Pa)
dXGa/dt=(Kbxg*XG)+(Knaxp1*NFkBa*XG)-(Kdxg*XGa)
dMEKKa/dt=(Kbkk1*MEKK)+(Ktkk1*MEKK*TNFR1a)-(Kdkk1*MEKKa)+(Krk4*MEKK*ROSa)-(Kaik4*pAKT*MEKKa)
dpJNK/dt=(Kbjnk*JNK)+(Kcajk*JNK*C1Pa)-((Keij*pERK*pJNK)/(Keij1+Keij2*pERK))-(Kdjnk*pJNK)+(Km4aj*JNK*MEKKa)-(Kxij*XGa*pJNK)
#----------------------------------------------------------------------------------------------------------------------------------------
# AKT module
#----------------------------------------------------------------------------------------------------------------------------------------
dNFkBa/dt=((Kbnf*NFkB)/(1+Kinh*INHB1))+((Ktnf*NFkB*TNFR1a)/(1+Kinh*INHB1))+(Kjan*pJNK*NFkB)-(Kpin*PTENa*NFkBa)-(Kdnf*NFkBa)
dPTENa/dt=(Kbpt*PTEN)-(Knip*NFkBa*PTENa)-(Kdpt*PTENa)
dPI3Ka/dt=(Kbp3k*PI3K)+((Ktpi1*PI3K*TNFR1a))-(Kpip3*PI3Ka*PTENa)-(Kbdp3k*PI3Ka)
dpAKT/dt=(Kbak*AKT)+(Kpaak*PI3Ka*AKT)-(Kbdak*pAKT)+(Kjaa*AKT*pJNK)-(Kcpia*CAPPa*pAKT)+(Kxaa*AKT*Xa)
dCERa/dt=(Kbcer*CER)+(Ktcr*CER*TNFR1a)- (Kdcer*CERa)-(Kpicr*PI3Ka*CERa)
dCAPPa/dt=(Kbcpp*CAPP)+(Kacp*CAPP*CERa)-(Kdcpp*CAPPa)
dXa/dt=(Kbx*Xi)-(Knix*NFkBa*Xa)-(Kdx*Xa)
#----------------------------------------------------------------------------------------------------------------------------------------
# ERK module
#----------------------------------------------------------------------------------------------------------------------------------------
dRAFa/dt=(Kbraf*RAF)+(Ktnar*TNFR1a*RAF)-(Keir*RAFa*pERK)-(Kair*pAKT*RAFa)-(Kdbraf*RAFa)
dROSa/dt=(Kbros*ROS)+(Kjar*ROS*pJNK)-(Kdros*ROSa)
dpERK/dt=(Kberk*ERK)+(Kmae*ERK*RAFa)+(Kpaer*PI3Ka*ERK)+(Kjae*ERK*pJNK)-(Kdbrk*pERK)
#----------------------------------------------------------------------------------------------------------------------------------------
# Caspase 3 module
#----------------------------------------------------------------------------------------------------------------------------------------
dCsps3a/dt=(Kbcs3*Csps3)+(Kjacs3*Csps3*pJNK)/(Kjac2+(n1*pJNK))+(Ktcs*TNFR1a*Csps3)-(Knics3*NFkBa*Csps3a)\
-(Kaics3*Csps3a*pAKT**Kn2)/(Kaic1+(Kaic2*pAKT**Kn2))-((Keics3*pERK*Csps3a)/(Keic1+(Keic2*pERK)))-(Kdbcs3*Csps3a)
#========================================================================================================================================
#========================================================================================================================================

CER=(CERT-CERa)
CAPP=(CAPPT-CAPPa)
Xi=(XT- Xa)
TNFR1=TNFR1T-TNFR1a
C1P=C1PT-C1Pa
XG=XGT-XGa
JNK=JNKT-pJNK
NFkB=NFkBT-NFkBa
PTEN=PTENT-PTENa
PI3K=PI3KT-PI3Ka
AKT=AKTT-PAKT
ERK=ERKT-pERK
Csps3=Csps3T-Csps3a
RAF=RAFT-RAFa
ROS=ROST-ROSa
MEKK=MEKKT-MEKKa

baAKT=(Kbak*AKTT)/(Kbdak + Kbak)
baJNK=(Kbjnk*JNKT)/(Kbjnk+Kdjnk)
baCS3=(Kbcs3*Csps3T)/(Kbcs3+Kdbcs3)

aux baAKT=(Kbak*AKTT)/(Kbdak + Kbak)
aux baJNK=(Kbjnk*JNKT)/(Kbjnk+Kdjnk)
aux baCS3=(Kbcs3*Csps3T)/(Kbcs3+Kdbcs3)

# Calculating Fold Change
aux FCJNK=s*(pJNK/baJNK)
aux FCAKT=s1*(pAKT/baAKT)
aux FCCs3=s2*(Csps3a/baCS3)
#========================================================================================================================================
# Module for generating Reaction fluxes in Fig 3, S10 Fig, Fig II in S3 Text
#========================================================================================================================================
##
aux J1 = (Kbjnk*JNK)
aux J2 = (Kcajk*JNK*C1Pa)
aux J3 = -((Keij*pERK*pJNK)/(Keij1+Keij2*pERK))
aux J4 = -(Kdjnk*pJNK)
aux J5 = (Km4aj*JNK*MEKKa)
aux J6 = -(Kxij*XGa*pJNK)
##
aux A1 = (Kbak*AKT)
aux A2 = (Kpaak*PI3Ka*AKT)
aux A3 = -(Kbdak*pAKT)
aux A4 = (Kjaa*AKT*pJNK)
aux A5 = -(Kcpia*CAPPa*pAKT)
aux A6 = (Kxaa*AKT*Xa)
##
aux C1 = (Kbcs3*Csps3)
aux C2 = (Kjacs3*Csps3*pJNK)/(Kjac2+(n1*pJNK))
aux C3 = (Ktcs*TNFR1a*Csps3)
aux C4 = -(Knics3*NFkBa*Csps3a)
aux C5 = -(Kaics3*Csps3a*pAKT**Kn2)/(Kaic1+(Kaic2*pAKT**Kn2))
aux C6 = ((Keics3*pERK*Csps3a)/(Keic1+(Keic2*pERK)))
aux C7 = -(Kdbcs3*Csps3a)
##
aux n1 = ((Kbnf*NFkB)/(1+Kinh*INHB1))
aux n2 = ((Ktnf*NFkB*TNFR1a)/(1+Kinh*INHB1))
aux n3 = (Kjan*pJNK*NFkB)
aux n4 = -(Kpin*PTENa*NFkBa)
aux n5 = -(Kdnf*NFkBa)
#========================================================================================================================================
#========================================================================================================================================
# Module for generating branches (Double R) in Fig 4 and S11 Fig
#========================================================================================================================================
#========================================================================================================================================
#-----------------------------------------------------Defining all fluxes----------------------------------------------------------------
tf1 = (Ktnf1*TNFR1*TNF) 
Cp2 = (Kcac1*C1P*CERa)  
Xx2= (Knaxp1*NFkBa*XG)  
Mk2 = (Ktkk1*MEKK*TNFR1a) 
Mk4 = (Krk4*MEKK*ROSa)
Mk5 = (Kaik4*pAKT*MEKKa)
J2 = (Kcajk*JNK*C1Pa)   
J3 = ((Keij*pERK*pJNK)/(Keij1+Keij2*pERK)) 
J5 = (Km4aj*JNK*MEKKa) 
J6 = (Kxij*XGa*pJNK)
n2 = ((Ktnf*NFkB*TNFR1a)/(1+Kinh*INHB1))
n3 = (Kjan*pJNK*NFkB)
n4 = (Kpin*PTENa*NFkBa)
P2 = (Knip*NFkBa*PTENa)
Pi2 = ((Ktpi1*PI3K*TNFR1a))
Pi3 = (Kpip3*PI3Ka*PTENa)
A2 = (Kpaak*PI3Ka*AKT)
A4 = (Kjaa*AKT*pJNK)
A5 = (Kcpia*CAPPa*pAKT)
A6 = (Kxaa*AKT*Xa)
Cr2 = (Ktcr*CER*TNFR1a)
Cr4 = (Kpicr*PI3Ka*CERa)
Ca2 = (Kacp*CAPP*CERa)
B2 = (Knix*NFkBa*Xa)
Rf2 = (Ktnar*TNFR1a*RAF)
Rf3 = (Keir*RAFa*pERK)
Rf4 = (Kair*pAKT*RAFa)
Rr2 = (Kjar*ROS*pJNK)
E2 = (Kmae*ERK*RAFa)
E3 = (Kpaer*PI3Ka*ERK)
E4 = (Kjae*ERK*pJNK)
C2 = (Kjacs3*Csps3*pJNK)/(Kjac2+(n1*pJNK))
C3 = (Ktcs*TNFR1a*Csps3)
C4 = (Knics3*NFkBa*Csps3a)
C5 = (Kaics3*Csps3a*pAKT**Kn2)/(Kaic1+(Kaic2*pAKT**Kn2))
C6 = ((Keics3*pERK*Csps3a)/(Keic1+(Keic2*pERK)))
#========================================================================================================================================
#--------------------------------------------------------OUTPUT and INPUT fluxes on bearable---------------------------------------------
t_out_C1P=J2
t_in_C1P=Cp2
t_out_XG=J6
t_in_XG=Xx2
t_out_MEKK=J5
t_in_MEKK=(Mk2+Mk4+Mk5)
t_out_JNK=(n3+C2+E4+Rr2+A4)
t_in_JNK=(J2+J3+J5+J6)
t_out_NFkB=(Xx2+P2+B2+C4)
t_in_NFkB=(n2+n3+n4)
t_out_PTEN=(n4+pi3)
t_in_PTEN=p2
t_out_PI3K=(E3+Cr4+A2)
t_in_PI3K=(Pi2+Pi3)
t_out_AKT=(Mk5+Rf4+C5)
t_in_AKT=(A2+A4+A5+A6)
t_out_CER=(Ca2+Cp2)
t_in_CER=(Cr2+Cr4)
t_out_BCL2=a6
t_in_BCL2=B2
t_out_RAF=E2
t_in_RAF=(Rf2+Rf3+Rf4)
t_out_ROS=Mk4
t_in_ROS=Rr2
t_out_ERK=(Rf3+C6+J3)
t_in_ERK=(E2+E3+E4)
t_out_Cs3=0
t_in_Cs3=(C2+C3+C4+C5+C6)
t_out_CAPP=A5
t_in_CAPP=Ca2
#----------------------------------------------------------------------------------------------------------------------------------------
#  Generating branches (Double R) in Fig 4A and S11A Fig
#----------------------------------------------------------------------------------------------------------------------------------------
#.......................................................................
##		Branch analysis NFkB AKT Br1:NFkB=>BCL2=>AKT
#.......................................................................
RFNB1 = B2*((t_out_BCL2)/(t_in_BCL2))
RFBA1 = a6*((t_out_AKT)/(t_in_AKT))
RFBr1na = RFNB1*RFBA1
aux RFBr1na = RFNB1*RFBA1
#........................................................................
##		Branch analysis NFkB AKT Br2: NFkB=>PTEN=>PI3K=>AKT
#........................................................................
RFNNP2 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPPP2 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPA2 = a2*((t_out_AKT)/(t_in_AKT))
RFBr2na = RFNNP2*RFPPP2*RFPA2
aux RFBr2na = RFNNP2*RFPPP2*RFPA2
#..........................................................................
##		Branch analysis NFkB AKT Br3: NFkB=>XG=>JNK=>AKT
#..........................................................................
RFNX3 = Xx2*((t_out_XG)/(t_in_XG))
RFXJ3 = J6*((t_out_JNK)/(t_in_JNK))
RFJA3 = a4*((t_out_AKT)/(t_in_AKT))
RFBr3na = RFNX3*RFXJ3*RFJA3
aux RFBr3na = RFNX3*RFXJ3*RFJA3
#.............................................................................
##		Branch analysis NFkB AKT Br4: NFkB=>PTEN=>PI3K=>ERK=>JNK=>AKT
#.............................................................................
RFNNP4 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPPP4 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPE41 = E3*((t_out_ERK)/(t_in_ERK))
RFEJ41 = J3*((t_out_JNK)/(t_in_JNK))
RFJA4 = a4*((t_out_AKT)/(t_in_AKT))
RFBr4na = RFNNP4*RFPPP4*RFPE41*RFEJ41*RFJA4
aux RFBr4na = RFNNP4*RFPPP4*RFPE41*RFEJ41*RFJA4
#..............................................................................
##		Branch analysis NFkB JNK Br5: NFkB=>PTEN=>PI3K=>CER=>CAPP=>AKT
#..............................................................................
RFNP5 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPP5 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPC5 =Cr4*((t_out_CER)/(t_in_CER))
RFCC5 = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA5 = A5*((t_out_AKT)/(t_in_AKT))
RFBr5na = RFNP5*RFPP5*RFPC5*RFCC5*RFCA5
aux RFBr5na = RFNP5*RFPP5*RFPC5*RFCC5*RFCA5
#..................................................................................
##		Branch analysis NFkB JNK Br6: NFkB=>PTEN=>PI3K=>CER=>C1P=>JNK=>AKT
#...................................................................................
RFNNP6 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPPP6 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPC61 =Cr4*((t_out_CER)/(t_in_CER))
RFCC61 = Cp2*((t_out_C1P)/(t_in_C1P))
RFCJ61 = j2*((t_out_JNK)/(t_in_JNK))
RFJJA6 = a4*((t_out_AKT)/(t_in_AKT))
RFBr6na = RFNNP6*RFPPP6*RFPC61*RFCC61*RFCJ61*RFJJA6
aux RFBr6na = RFNNP6*RFPPP6*RFPC61*RFCC61*RFCJ61*RFJJA6
#----------------------------------------------------------------------------------------------------------------------------------------
#  Generating branches (Double R) in Fig 4B and S11B Fig
#----------------------------------------------------------------------------------------------------------------------------------------
#...................................................................................
##		Branch analysis TNFR1 AKT Br1: TNFR1=>PI3K=>AKT
#...................................................................................
RFTP1 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFPA1 = a2*((t_out_AKT)/(t_in_AKT))
RFBr1ta = RFTP1*RFPA1
aux RFBr1ta = RFTP1*RFPA1
#...................................................................................
##		Branch analysis TNFR1 AKT Br2: TNFR1=>MKK=>JNK=>AKT
#...................................................................................
RFTM2 = Mk2*((t_out_MEKK)/(t_in_MEKK))
RFMJ2 = J5*((t_out_JNK)/(t_in_JNK))
RFJA2 = A4*((t_out_AKT)/(t_in_AKT))
RFBr2ta = RFTM2*RFMJ2*RFJA2
aux RFBr2ta = RFTM2*RFMJ2*RFJA2
#...................................................................................
##		Branch analysis TNFR1 AKT Br3: TNFR1=>CER=>CAPP=>AKT
#...................................................................................
RFTC31 =Cr2*((t_out_CER)/(t_in_CER))
RFCC31 = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA3 = A5*((t_out_AKT)/(t_in_AKT))
RFBr3ta = RFTC31*RFCC31*RFCA3
aux RFBr3ta = RFTC31*RFCC31*RFCA3
#...................................................................................
##		Branch analysis TNFR1 AKT Br4: TNFR1=>MKK=>JNK=>ROS=>MKK
#...................................................................................
RFTM4 = Mk2*((t_out_MEKK)/(t_in_MEKK))
RFMJ4 = J5*((t_out_JNK)/(t_in_JNK))
RFJR4 = Rr2*((t_out_ROS)/(t_in_ROS))
RFRM4 = Mk4*((t_out_MEKK)/(t_in_MEKK))
RFBr4ta = RFTM4*RFMJ4*RFJR4*RFRM4
aux RFBr4ta = RFTM4*RFMJ4*RFJR4*RFRM4
#...................................................................................
##		Branch analysis TNFR1 AKT Br5: TNFR1=>RAF=>ERK=>JNK=>AKT
#...................................................................................
RFTR5 = Rf2*((t_out_RAF)/(t_in_RAF))
RFREE5 = E2*((t_out_ERK)/(t_in_ERK))
RFEEJ5 = j3*((t_out_JNK)/(t_in_JNK))
RFJA5 = A4*((t_out_AKT)/(t_in_AKT))
RFBr5ta = RFTR5*RFREE5*RFEEJ5*RFJA5
aux RFBr5ta = RFTR5*RFREE5*RFEEJ5*RFJA5
#...................................................................................
##		Branch analysis TNFR1 AKT Br6: TNFR1=>PI3K=>ERK=>JNK=>AKT
#...................................................................................
RFTP61 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFPE6 = E3*((t_out_ERK)/(t_in_ERK))
RFEJ6 = j3*((t_out_JNK)/(t_in_JNK))
RFJA6 = A4*((t_out_AKT)/(t_in_AKT))
RFBr6ta = RFTP61*RFPE6*RFEJ6*RFJA6
aux RFBr6ta = RFTP61*RFPE6*RFEJ6*RFJA6
#...................................................................................
##		Branch analysis TNFR AKT Br7: TNFR1=>CER=>C1P=>JNK=>AKT
#...................................................................................
RFTC77 = Cr2*((t_out_CER)/(t_in_CER))
RFCC77 = Cp2*((t_out_C1P)/(t_in_C1P))
RFCJ7 = j2*((t_out_JNK)/(t_in_JNK))
RFJA7 = a4*((t_out_AKT)/(t_in_AKT))
RFBr7ta = RFTC77*RFCC77*RFCJ7*RFJA7
aux RFBr7ta = RFTC77*RFCC77*RFCJ7*RFJA7
#...................................................................................
##		Branch analysis TNFR1 AKT Br8: TNFR1=>PI3K=>CER=>CAPP=>AKT
#...................................................................................
RFTP88 = n2*((t_out_PI3K)/(t_in_PI3K))
RFPCC8 = Cr4*((t_out_CER)/(t_in_CER))
RFFCC8 = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA81 = A5*((t_out_AKT)/(t_in_AKT))
RFBr8ta = RFTP88*RFPCC8*RFFCC8*RFCA81
aux RFBr8ta = RFTP88*RFPCC8*RFFCC8*RFCA81
#...................................................................................
##		Branch analysis TNFR1 AKT Br9: TNFR1=>PI3K=>CER=>C1P=>JNK=>AKT
#...................................................................................
RFTP9 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFPC91 = Cr4*((t_out_CER)/(t_in_CER))
RFCC91 = Cp2*((t_out_C1P)/(t_in_C1P))
RFCJ9 = j2*((t_out_JNK)/(t_in_JNK))
RFJA9 = a4*((t_out_AKT)/(t_in_AKT))
RFBr9ta = RFTP9*RFPC91*RFCC91*RFCJ9*RFJA9
aux RFBr9ta = RFTP9*RFPC91*RFCC91*RFCJ9*RFJA9
#----------------------------------------------------------------------------------------------------------------------------------------
#  Generating branches (Double R) in Fig 4C and S11C Fig
#----------------------------------------------------------------------------------------------------------------------------------------
#...................................................................................
##		Branch analysis NFkB JNK Br1:NFkB=>XG=>JNK
#...................................................................................
RFNX1 = Xx2*((t_out_XG)/(t_in_XG))
RFXJ1 = j6*((t_out_JNK)/(t_in_JNK))
RFBr1nj = RFNX1*RFXJ1
aux RFNX1 = Xx2*((t_out_XG)/(t_in_XG))
aux RFXJ1 = j6*((t_out_JNK)/(t_in_JNK))
aux RFBr1nj = RFNX1*RFXJ1
#...................................................................................
##		Branch analysis NFkB JNK Br2:NFkB=>PTEN=>PI3K=>ERK=>JNK
#...................................................................................
RFNP2 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPP2 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPE2 =E3*((t_out_ERK)/(t_in_ERK))
RFEJ21 = j3*((t_out_JNK)/(t_in_JNK))
RFBr2nj = RFNP2*RFPP2*RFPE2*RFEJ21
aux RFBr2nj = RFNP2*RFPP2*RFPE2*RFEJ21
#...................................................................................
##		Branch analysis NFkB JNK Br3: NFkB=>BCL2=>AKT=>MKK=>JNK
#...................................................................................
RFNB3 = B2*((t_out_BCL2)/(t_in_BCL2))
RFBA3 = a6*((t_out_AKT)/(t_in_AKT))
RFAM3 = Mk5*((t_out_MEKK)/(t_in_MEKK))
RFMJ3 = j5*((t_out_JNK)/(t_in_JNK))
RFBr3nj = RFNB3*RFBA3*RFAM3*RFMJ3
aux RFBr3nj = RFNB3*RFBA3*RFAM3*RFMJ3
#...................................................................................
##		Branch analysis NFkB JNK Br4: NFkB=>PTEN=>PI3K=>CER=>C1P=>JNK
#...................................................................................
RFNP4 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPP4 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPC4 =Cr4*((t_out_CER)/(t_in_CER))
RFCC4 = Cp2*((t_out_C1P)/(t_in_C1P))
RFCJ4 = j2*((t_out_JNK)/(t_in_JNK))
RFBr4nj = RFNP4*RFPP4*RFPC4*RFCC4*RFCJ4
aux RFBr4nj = RFNP4*RFPP4*RFPC4*RFCC4*RFCJ4
#...................................................................................
##		Branch analysis NFkB JNK Br5: NFkB=>BCL2=>AKT=>RAF=>ERK=>JNK
#...................................................................................
RFNB5 = B2*((t_out_BCL2)/(t_in_BCL2))
RFBA5 = a6*((t_out_AKT)/(t_in_AKT))
RFAR5 = Rf4*((t_out_RAF)/(t_in_RAF))
RFRE5 = E2*((t_out_ERK)/(t_in_ERK))
RFEJ5 = j3*((t_out_JNK)/(t_in_JNK))
RFBr5nj = RFNB5*RFBA5*RFAR5*RFRE5*RFEJ5
aux RFBr5nj = RFNB5*RFBA5*RFAR5*RFRE5*RFEJ5
#...................................................................................
##		Branch analysis NFkB AKT Br6: NFkB=>PTEN=>PI3K=>AKT=>MKK=>JNK
#...................................................................................
RFNP6 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPP6 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPA6 = a2*((t_out_AKT)/(t_in_AKT))
RFAM6 = Mk5*((t_out_MEKK)/(t_in_MEKK))
RFMJ6 = j5*((t_out_JNK)/(t_in_JNK))
aux RFBr6nj = RFNP6*RFPP6*RFPA6*RFAM6*RFMJ6
#...................................................................................
##		Branch analysis NFkB AKT Br7: NFkB=>PTEN=>PI3K=>AKT=>RAF=>ERK=>JNK
#...................................................................................
RFNP7 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPP7 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPA7 = a2*((t_out_AKT)/(t_in_AKT))
RFAR7 = Rf4*((t_out_RAF)/(t_in_RAF))
RFRE7 = E2*((t_out_ERK)/(t_in_ERK))
RFEJ7 = j3*((t_out_JNK)/(t_in_JNK))
RFBr7nj = RFNP7*RFPP7*RFPA7*RFAR7*RFRE7*RFEJ7
aux RFBr7nj = RFNP7*RFPP7*RFPA7*RFAR7*RFRE7*RFEJ7
#...................................................................................
##	    Branch analysis NFkB JNK Br8: NFkB=>PTEN=>PI3K=>CER=>CAPP=>AKT=>MKK=>JNK
#...................................................................................
RFNP8 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPP8 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPC8 =Cr4*((t_out_CER)/(t_in_CER))
RFCC8 = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA8 = A5*((t_out_AKT)/(t_in_AKT))
RFAM8 = Mk5*((t_out_MEKK)/(t_in_MEKK))
RFMJ8 = j5*((t_out_JNK)/(t_in_JNK))
RFBr8nj = RFNP8*RFPP8*RFPC8*RFCC8*RFCA8*RFAM8*RFMJ8
aux RFBr8nj = RFNP8*RFPP8*RFPC8*RFCC8*RFCA8*RFAM8*RFMJ8
#........................................................................................
##	    Branch analysis NFkB JNK Br9: NFkB=>PTEN=>PI3K=>CER=>CAPP=>AKT=>RAF=>ERK=>JNK
#........................................................................................
RFNP9 = P2*((t_out_PTEN)/(t_in_PTEN))
RFPP9 = Pi3*((t_out_PI3K)/(t_in_PI3K))
RFPC9j =Cr4*((t_out_CER)/(t_in_CER))
RFCC9j = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA9j = A5*((t_out_AKT)/(t_in_AKT))
RFAR9j = Rf4*((t_out_RAF)/(t_in_RAF))
RFRE9j = E2*((t_out_ERK)/(t_in_ERK))
RFEJ9j = j3*((t_out_JNK)/(t_in_JNK))
RFBr9nj = RFNP9*RFPP9*RFPC9j*RFCC9j*RFCA9j*RFAR9j*RFRE9j*RFEJ9j
aux RFBr9nj = RFNP9*RFPP9*RFPC9j*RFCC9j*RFCA9j*RFAR9j*RFRE9j*RFEJ9j
#----------------------------------------------------------------------------------------------------------------------------------------
#  Generating branches (Double R) in Fig 4D and S11D Fig 
#----------------------------------------------------------------------------------------------------------------------------------------
#...................................................................................
##		Branch analysis TNFR1 JNK Br1: TNFR1=>MKK=>JNK
#...................................................................................
RFTM1 = Mk2*((t_out_MEKK)/(t_in_MEKK))
RFMJ1 = j5*((t_out_JNK)/(t_in_JNK))
RFBr1tj = RFTM1*RFMJ1
aux RFBr1tj = RFTM1*RFMJ1
#...................................................................................
##		Branch analysis TNFR1 JNK Br2:TNFR1=>RAF=>ERK=>JNK
#...................................................................................
RFTR2 = Rf2*((t_out_RAF)/(t_in_RAF))
RFRE2 = E2*((t_out_ERK)/(t_in_ERK))
RFEJ2 = j3*((t_out_JNK)/(t_in_JNK))
RFBr2tj = RFTR2*RFRE2*RFEJ2
aux RFBr2tj = RFTR2*RFRE2*RFEJ2
#...................................................................................
##		Branch analysis TNFR1 JNK Br3: TNFR1=>CER=>C1P=>JNK
#...................................................................................
RFTC3 =Cr2*((t_out_CER)/(t_in_CER))
RFCC3 = Cp2*((t_out_C1P)/(t_in_C1P))
RFCJ3 = j2*((t_out_JNK)/(t_in_JNK))
RFBr3tj = RFTC3*RFCC3*RFCJ3
aux RFBr3tj = RFTC3*RFCC3*RFCJ3
#...................................................................................
##		Branch analysis TNFR1 JNK Br4: TNFR1=>PI3K=>ERK=>JNK
#....................................................................................
RFTP4 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFPE4 =E3*((t_out_ERK)/(t_in_ERK))
RFEJ4 = j3*((t_out_JNK)/(t_in_JNK))
RFBr4tj = RFTP4*RFPE4*RFEJ4
aux RFBr4tj = RFTP4*RFPE4*RFEJ4
#...................................................................................
##		Branch analysis TNFR1 JNK Br5: TNFR1=>PI3K=>AKT=>MKK=>JNK
#...................................................................................
RFTP5 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFPA5 = A2*((t_out_AKT)/(t_in_AKT))
RFAM5 = Mk5*((t_out_MEKK)/(t_in_MEKK))
RFMJ5 = j5*((t_out_JNK)/(t_in_JNK))
RFBr5tj = RFTP5*RFPA5*RFAM5*RFMJ5
aux RFBr5tj = RFTP5*RFPA5*RFAM5*RFMJ5
#...................................................................................
##		Branch analysis TNFR1 JNK Br6: TNFR1=>PI3K=>CER=>C1P=>JNK
#...................................................................................
RFTP6 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFPC6 =Cr4*((t_out_CER)/(t_in_CER))
RFCC6 = Cp2*((t_out_C1P)/(t_in_C1P))
RFCJ6 = j2*((t_out_JNK)/(t_in_JNK))
RFBr6tj = RFTP6*RFPC6*RFCC6*RFCJ6
aux RFBr6tj = RFTP6*RFPC6*RFCC6*RFCJ6
#...................................................................................
##		Branch analysis TNFR1 JNK Br7: TNFR1=>CER=>CAPP=>AKT=>MKK=>JNK
#...................................................................................
RFTC7 =Cr2*((t_out_CER)/(t_in_CER))
RFCC7 = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA7 = A5*((t_out_AKT)/(t_in_AKT))
RFAM7 = Mk5*((t_out_MEKK)/(t_in_MEKK))
RFMJ7 = j5*((t_out_JNK)/(t_in_JNK))
RFBr7tj = RFTC7*RFCC7*RFCA7*RFAM7*RFMJ7
aux RFBr7tj = RFTC7*RFCC7*RFCA7*RFAM7*RFMJ7
#...................................................................................
##		Branch analysis TNFR1 JNK Br8: TNFR1=>PI3K=>AKT=>RAF=>ERK=>JNK
#...................................................................................
RFTP8 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFBA8 = A2*((t_out_AKT)/(t_in_AKT))
RFAR8 = Rf4*((t_out_RAF)/(t_in_RAF))
RFRE8 = E2*((t_out_ERK)/(t_in_ERK))
RFEJ8 = j3*((t_out_JNK)/(t_in_JNK))
RFBr8tj = RFTP8*RFBA8*RFAR8*RFRE8*RFEJ8
aux RFBr8tj = RFTP8*RFBA8*RFAR8*RFRE8*RFEJ8
#...................................................................................
##		Branch analysis TNFR1 JNK Br9: TNFR1=>CER=>CAPP=>AKT=>RAF=>ERK=>JNK
#...................................................................................
RFPC9 =Cr2*((t_out_CER)/(t_in_CER))
RFCC9 = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA9 = A5*((t_out_AKT)/(t_in_AKT))
RFAR9 = Rf4*((t_out_RAF)/(t_in_RAF))
RFRE9 = E2*((t_out_ERK)/(t_in_ERK))
RFEJ9 = j3*((t_out_JNK)/(t_in_JNK))
RFBr9tj = RFPC9*RFCC9*RFCA9*RFAR9*RFRE9*RFEJ9
aux RFBr9tj = RFPC9*RFCC9*RFCA9*RFAR9*RFRE9*RFEJ9
#...................................................................................
##		Branch analysis TNFR1 JNK Br10: TNFR1=>PI3K=>CER=>CAPP=>AKT=>MKK=>JNK
#...................................................................................
RFTP10 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFPC10 =Cr4*((t_out_CER)/(t_in_CER))
RFCC10 = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA10 = A5*((t_out_AKT)/(t_in_AKT))
RFAM10 = Mk5*((t_out_MEKK)/(t_in_MEKK))
RFMJ10 = j5*((t_out_JNK)/(t_in_JNK))
RFBra10tj = RFTP10*RFPC10*RFCC10*RFCA10*RFAM10*RFMJ10
aux RFBra10tj = RFTP10*RFPC10*RFCC10*RFCA10*RFAM10*RFMJ10
#..........................................................................................
##		Branch analysis TNFR1 JNK Br11: TNFR1=>PI3K=>CER=>CAPP=>AKT=>RAF=>ERK=>JNK
#..........................................................................................
RFTP11 = Pi2*((t_out_PI3K)/(t_in_PI3K))
RFPC11 =Cr4*((t_out_CER)/(t_in_CER))
RFCC11 = Ca2*((t_out_CAPP)/(t_in_CAPP))
RFCA11 = A5*((t_out_AKT)/(t_in_AKT))
RFAR11 = Rf4*((t_out_RAF)/(t_in_RAF))
RFRE11 = E2*((t_out_ERK)/(t_in_ERK))
RFEJ11 = j3*((t_out_JNK)/(t_in_JNK))
RFBra11tj = RFTP11*RFPC11*RFCC11*RFCA11*RFAR11*RFRE11*RFEJ11
aux RFBra11tj = RFTP11*RFPC11*RFCC11*RFCA11*RFAR11*RFRE11*RFEJ11
#
# Parameters of the Model
#========================================================================================================================================
p Ktnf1=0.0337, Ktnf2=0.00015, Kbase=0.0001,
p Kbc1=0.00346, Kcac1=0.00341, Kcac2=0.0005,
p Kbxg=0.03223, Knaxp1=0.0183, Kdxg=0.1023, Kxij=0.602,
p Kbjnk=0.00368, Kcajk=0.102, Km4aj=0.0253,
p Keij=0.0285, Keij1=1.36, Keij2=0.0895, Kdjnk=0.0839,
p Kbnf=2.997, Ktnf=0.0015, Kinh=0.833, 
p Kjan=0.00011, Kpin=0.0486, Kdnf=0.238,
p Kbpt=0.0139, Knip=0.0996, Kdpt=0.00023,
p Kbp3k=0.000149, Ktpi1=0.0144, Kpip3=1.56, Kbdp3k=0.00073,
p Kbak=0.025, Kpaak=0.259, Kjaa=0.998, Kbdak=7.38,
p Kbraf=0.0418, Ktnar=0.000283, Keir=0.0069, Kair=2.38, Kdbraf=0.974
p Kbros=0.0474, Kjar=0.0735, Kdros=0.256
p Kberk=0.00015, Kmae=0.0018, Kpaer=0.00063, Kjae=0.0104, Kdbrk=0.0174,
p Kbcs3=0.0045, Kjacs3=0.0113, Ktcs=0.04316, Knics3=0.577,
p Kaics3=0.00378, Keics3=0.00021, Keic1=0.7635, Keic2=4.62, Kn2=11.44,
p Kbkk1=0.00139, Ktkk1=0.0768, Kdkk1=0.079, Krk4=0.1, Kaik4=0.005,
p Kdbcs3=0.1196, n1=0.0186, Kaic1=0.0077, Kaic2=0.00076, Kjac2=0.000162,
p Kbcer=0.00752, Ktcr=0.00665, Kdcer=0.001056, Kpicr=0.108,
p Kbcpp=0.0153, Kacp=0.069, Kdcpp=0.09, Kcpia=1.09,
p Kbx=0.933, Knix=11.36, Kdx=0.059, Kxaa=0.494,
p C1PT=10, TNF=6, TNFR1T=10, XGT=10, XT=10
p JNKT=10, NFkBT=10, PTENT=10, PI3KT=10
p AKTT=10, ERKT=50, Csps3T=10, INHB1=60, ROST=10
p RAFT=10, MEKKT=10, CERT=10, CAPPT=10 
p s=0.429, s1=0.0349, s2=0.362
#========================================================================================================================================
# Numerical Control parameters for time-course simulation
#========================================================================================================================================
@ TOTAL=24, METHOD=stiff, BOUND=100000000, Tolerence=0.0000000001

done